plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'project-report'
    id 'org.beryx.jlink'
}

dependencies {
    implementation project(path: ':lib-api')
    implementation project(path: ':lib-common')
    implementation project(path: ':lib-season1')
    implementation project(path: ':lib-season2')
    implementation project(path: ':lib-season3')

    implementation cli.picocli
    implementation cli.jackson.dataformat.yaml
}

tasks.distTar.enabled = false

application {
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
    mainModule = 'org.sehkah.ddon.tools.extractor.cli'
    mainClass = 'org.sehkah.ddon.tools.extractor.cli.Main'
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

jlink {
    options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
    launcher {
        name = rootProject.name
        jvmArgs = ['-Dfile.encoding=UTF-8']
    }
    jpackage {
        jvmArgs = ['-Dfile.encoding=UTF-8']
        skipInstaller = true
        imageOptions += ['--win-console']
        installerOptions += [
                '--vendor', 'Sehkah',
                '--app-version', version
        ]
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser', '--win-menu'
            ]
        }
    }
    forceMerge('jackson', 'slf4j', 'log4j', 'snakeyaml')
    mergedModule {
        additive = true
        uses 'com.fasterxml.jackson.databind.Module'
        uses 'org.apache.logging.log4j.util.PropertySource'
        uses 'org.apache.logging.log4j.core.util.ContextDataProvider'
        uses 'org.apache.logging.log4j.core.util.WatchEventService'
        uses 'org.apache.logging.log4j.spi.Provider'
        uses 'org.apache.logging.log4j.message.ThreadDumpMessage.ThreadInfoFactory'
    }
}
