plugins {
    id 'org.javamodularity.moduleplugin' apply false
    id 'io.freefair.lombok' apply false
    id 'com.github.ben-manes.versions'
}

allprojects {
    wrapper {
        distributionType = Wrapper.DistributionType.ALL
    }
}

subprojects {
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(sourceCompatibility.toString())
        }
        withJavadocJar()
        withSourcesJar()
    }

    testing {
        suites {
            test {
                useJUnitJupiter(common.versions.junit)
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    dependencies {
        implementation log.bundles.logging.implementation
        runtimeOnly log.bundles.logging.runtimeOnly
    }

    javadoc {
        options.addBooleanOption('html5', true)
        source = sourceSets.main.allJava
        classpath = configurations.compileClasspath
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = rootProject.name + '-' + project.name

                from components.java

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    name = rootProject.name + '-' + project.name
                    description = 'ddon-extractor libraries'
                    url = 'https://github.com/ddon-research/ddon-extractor'

                    licenses {
                        license {
                            name = 'GNU Affero General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
                        }
                    }

                    developers {
                        developer {
                            id = 'sehkah'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/ddon-research/ddon-extractor.git'
                        developerConnection = 'scm:git:https://github.com/ddon-research/ddon-extractor.git'
                        url = 'https://github.com/ddon-research/ddon-extractor/tree/main'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
                def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
